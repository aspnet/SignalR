buildscript {
    repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "com.diffplug.spotless:spotless-plugin-gradle:3.14.0"
  }
}

plugins {
    id 'java'
    id 'maven'
}

apply plugin: "com.diffplug.gradle.spotless"

group 'com.microsoft.signalr'

// If we're run from outside MSBuild, just assign a bogus dev version.
version project.findProperty('packageVersion') ?: "99.99.99-dev"

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    // add sonatype repository (temporary, due to java-8-parent being a snapshot)
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}

dependencies {
    implementation 'com.microsoft.maven:java-8-parent:8.0.0-SNAPSHOT'

    // dependency versions imported from java-8-parent POM imported above
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testCompile 'org.junit.jupiter:junit-jupiter-params'
    testRuntime 'org.junit.jupiter:junit-jupiter-engine'
    implementation 'com.google.code.gson:gson'
    implementation 'com.squareup.okhttp3:okhttp'
    implementation 'io.reactivex.rxjava2:rxjava'
    implementation 'org.slf4j:slf4j-api'
}

spotless {
    java {
        licenseHeader '// Copyright (c) .NET Foundation. All rights reserved.\n' +
                '// Licensed under the Apache License, Version 2.0. See License.txt in the project root for license information.\n\n' // License header

        importOrder 'java', 'javax', 'org', 'com', 'com.diffplug', ''	// A sequence of package names

        replace      'Not enough space after if', 'if(', 'if ('
        replace      'Not enough space after else', 'else{', 'else {'
        replace      'Not enough space before else', '}else', '} else '
        replace      'Not enough space after try', 'try{', 'try {'
        replace      'Not enough space before finally', '}finally', '} finally'
        replace      'Not enough space after finally', 'finally{', 'finally {'
        replace      'Not enough space after )', '){', ') {'
        replace      'Not enough space after for', 'for(', 'for ('
        replace      'Not enough space after while', 'while (', 'while ('
        replace      'Not enough space after switch', 'switch(', 'switch ('
        replace      'Not enough space after do', 'do{', 'do {'
        replaceRegex 'Too much space after if', 'if +\\(', 'if ('
        trimTrailingWhitespace()
        indentWithSpaces(4)
        removeUnusedImports() // removes any unused imports
    }
}

test {
    useJUnitPlatform()
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

task generatePOM {
    pom {
        project {
            inceptionYear '2018'
            description 'ASP.NET Core SignalR Client for Java applications'
            url 'https://github.com/aspnet/SignalR'
            name groupId + ':' + artifactId
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                connection 'scm:git:git://github.com/aspnet/SignalR.git'
                developerConnection 'scm:git:git://github.com/aspnet/SignalR.git'
                url 'http://github.com/aspnet/SignalR/tree/master'
            }
            developers {
                developer {
                    id 'microsoft'
                    name 'Microsoft'
                }
            }
        }
    }.writeTo("${buildDir}/libs/signalr-${project.version}.pom")
}

task createPackage(dependsOn: [jar,sourceJar,javadocJar,generatePOM])
